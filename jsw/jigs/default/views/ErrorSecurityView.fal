load core.View

/**
 * Fallback for handling basic security violations.
 *
 * @author 	Vladimir Florentino
 * @package Jigsaw
 * @since 	0.1.0
 * @begin 	2009.5.13
 *
 * @param Jig jig - The Jig that owns this view.
 */
class ErrorSecurityView(jig) from View(jig)

	/**
	 * Constructor.
	 */
	init
		self.setTemplate('Shell', 'ErrorSecurity')
	end

	/**
	 * Process this view's logic.
	 *
	 * @return nil
	 */
	function process()
		// Set the color of the page.
		self.setValue('pageColor', '#F7F0A2')

		// This value was set by the framework.
		err = self.getValue(Framework.keyError)

		msg = err.description
		if not msg : msg = err.message
		if not msg : msg = '(nil)'

		// Pass the exception info to the template.
		self.setValue('message', 	msg)
		self.setValue('code', 		err.code)
		self.setValue('line', 		err.line)
		self.setValue('module', 	err.module)
		self.setValue('symbol', 	err.symbol)
		self.setValue('stackTrace', err.toString())
	end
end
